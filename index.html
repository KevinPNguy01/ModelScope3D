<!doctype html>
<html lang="en">
	<head>
		<meta charset="UTF-8" />
		<link rel="icon" type="image/svg+xml" href="/stanford_bunny.png" />
		<meta name="viewport" content="width=device-width, initial-scale=1.0" />
		<title>3D Model Viewer</title>
		<script
		src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.8.1/gl-matrix-min.js"
		integrity="sha512-zhHQR0/H5SEBL3Wn6yYSaTTZej12z0hVZKOv3TwCUXT1z5qeqGcXJLLrbERYRScEDDpYIJhPC1fk31gqR783iQ=="
		crossorigin="anonymous"
		defer>
		</script>
	</head>
	<body>
		<script id="lines-vertex-shader" type="x-shader/x-vertex">
			uniform mat4 uModel;
			uniform mat4 uView;
			uniform mat4 uProjection;
			uniform vec3 uScale;

			attribute vec3 aPosition;
			attribute vec3 aColor;

			varying vec3 vColor;

			void main() {
				gl_Position = uProjection * uView * uModel * vec4(aPosition * uScale, 1.0);
				vColor = aColor;
			}
		</script>
		<script id="lines-fragment-shader" type="x-shader/x-fragment">
			precision mediump float;

			varying vec3 vColor;

			void main() {
				gl_FragColor = vec4(vColor, 0.75);
			}
		</script>
		<script id="pbr-vertex-shader" type="x-shader/x-vertex">
			uniform mat4 uModelMatrix;
			uniform mat4 uViewMatrix;
			uniform mat4 uProjectionMatrix;
			uniform mat4 uNormalMatrix;      

			attribute vec3 aVertexPosition;     // Vertex position in object space
			attribute vec3 aVertexNormal;       // Vertex normal in object space
			attribute vec2 aTextureCoord;

			varying vec3 WorldPos;             	// Vertex position in world space
			varying vec3 Normal;              	// Vertex normal in world space
			varying vec2 TexCoords;

			void main(void) {
				WorldPos = vec3(uModelMatrix * vec4(aVertexPosition, 1.0));
				Normal = vec3(uNormalMatrix * vec4(aVertexNormal, 1.0));
				TexCoords = aTextureCoord;

				gl_Position = uProjectionMatrix * uViewMatrix * vec4(WorldPos, 1.0);
			}
		</script>
		<script id="pbr-fragment-shader" type="x-shader/x-fragment">
			precision highp float;

			struct Material {
				float metallic;
				float roughness;
				float ao;
			}; 

			struct DirLight {
				vec3 direction;
				vec3 color;
			};

			struct PointLight {    
				vec3 position; 
				vec3 color;
			};  

			uniform PointLight pointLight;
			uniform Material material;
			uniform DirLight dirLight;

			uniform sampler2D sampler;

			uniform vec3 camPos;

			varying vec2 TexCoords;
			varying vec3 WorldPos;
			varying vec3 Normal;

			const float PI = 3.14159265359;

			vec3 fresnelSchlick(float cosTheta, vec3 F0) {
				return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
			}

			float DistributionGGX(vec3 N, vec3 H, float roughness){
				float a      = roughness*roughness;
				float a2     = a*a;
				float NdotH  = max(dot(N, H), 0.0);
				float NdotH2 = NdotH*NdotH;
				
				float num   = a2;
				float denom = (NdotH2 * (a2 - 1.0) + 1.0);
				denom = PI * denom * denom;
				
				return num / denom;
			}

			float GeometrySchlickGGX(float NdotV, float roughness){
				float r = (roughness + 1.0);
				float k = (r*r) / 8.0;

				float num   = NdotV;
				float denom = NdotV * (1.0 - k) + k;
				
				return num / denom;
			}

			float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
			{
				float NdotV = max(dot(N, V), 0.0);
				float NdotL = max(dot(N, L), 0.0);
				float ggx2  = GeometrySchlickGGX(NdotV, roughness);
				float ggx1  = GeometrySchlickGGX(NdotL, roughness);
				
				return ggx1 * ggx2;
			}

			vec3 calculatePointLight(vec3 N, vec3 V) {
				// Calculate light direction and halfway vector
				vec3 L = normalize(pointLight.position - WorldPos);
				vec3 H = normalize(V + L);

				float distance = length(pointLight.position - WorldPos);
				float attenuation = 1.0 / (distance * distance);
				vec3 radiance = pointLight.color * attenuation;

				float NDF = DistributionGGX(N, H, material.roughness);        
				float G = GeometrySmith(N, V, L, material.roughness);
				
				vec3 F0 = vec3(0.04); 
    			F0 = mix(F0, texture2D(sampler, TexCoords).rgb, material.metallic);
				vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);

				vec3 kS = F;
				vec3 kD = vec3(1.0) - kS;
				kD *= 1.0 - material.metallic;

				vec3 numerator = NDF * G * F;
				float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.01;
				vec3 specular = numerator / denominator;

				float NdotL = max(dot(N, L), 0.0);
				return (kD * texture2D(sampler, TexCoords).rgb / PI + specular) * radiance * NdotL;
			}

			vec3 calculateDirectionalLight(vec3 N, vec3 V) {
				// Calculate light direction and halfway vector
				vec3 L = normalize(-dirLight.direction);
				vec3 H = normalize(V + L);

				vec3 radiance = dirLight.color;

				float NDF = DistributionGGX(N, H, material.roughness);        
				float G = GeometrySmith(N, V, L, material.roughness);
				
				vec3 F0 = vec3(0.04); 
    			F0 = mix(F0, texture2D(sampler, TexCoords).rgb, material.metallic);
				vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);

				vec3 kS = F;
				vec3 kD = vec3(1.0) - kS;
				kD *= 1.0 - material.metallic;

				vec3 numerator = NDF * G * F;
				float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.01;
				vec3 specular = numerator / denominator;

				float NdotL = max(dot(N, L), 0.0);
				return (kD * texture2D(sampler, TexCoords).rgb / PI + specular) * radiance * NdotL;
			}

			void main() {
				// Calculate normal and viewing directions
				vec3 N = normalize(Normal);
				vec3 V = normalize(camPos - WorldPos);

				if (dot(N, V) < 0.0) {
					N = -N;
				}

				vec3 Lo = calculatePointLight(N, V);
				Lo += calculateDirectionalLight(N, V);

				vec3 ambient = vec3(0.03) * texture2D(sampler, TexCoords).rgb * material.ao;
				vec3 color = ambient + Lo;

				color = color / (color + vec3(1.0));
				color = pow(color, vec3(1.0/2.2)); 
				gl_FragColor = vec4(color, 1.0);
			}
		</script>
		<div id="root"></div>
		<script type="module" src="/src/app/main.tsx"></script>
	</body>
</html>
